#compdef dscverify

# $ dscverify --version
# This is dscverify, from the Debian devscripts package, version 2.20.2
# ...

_dscverify() {
  local all_opts=(
    '--help[show the help message and exit]'
    '--version[show the version + copyright and exit]'
    '--no-default-keyrings[do not check against the default keyrings]'
    '*--keyring[add keyring to the list of keyrings used]:keyring:_files -g "*.{kbx,gpg}(-.)"'
    '(--nosigcheck --no-sig-check -u)'{--nosigcheck,--no-sig-check,-u}'[do not verify the GPG signature]'
    '--verbose[do not suppress GPG output]'
    '*:dsc file:_files -g "*.{changes,dsc,buildinfo}(-.)"'
  )

  local first_only=(
    '(--no-conf --noconf)'{--no-conf,--noconf}'[do not read the devscripts config file]'
  )

  if (( CURRENT == 2 )); then
    all_opts+=($first_only)
  fi

  _arguments \
    "$all_opts[@]"
}

_dscverify "$@"
